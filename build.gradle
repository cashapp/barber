buildscript {
  repositories {
    mavenCentral()
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  apply from: new File("./dependencies.gradle")

  dependencies {
    classpath dep.kotlinGradlePlugin
    classpath dep.spotlessPlugin
    classpath dep.mavenPublishGradlePlugin
    classpath dep.wireGradlePlugin
  }
}

allprojects {
  group = GROUP
  version = VERSION_NAME
}

subprojects { subProject ->
  apply plugin: 'java'
  apply plugin: 'kotlin'
  apply plugin: 'org.jetbrains.dokka'
  apply plugin: 'com.diffplug.gradle.spotless'
  apply plugin: 'com.squareup.wire'

  compileKotlin {
    kotlinOptions {
      jvmTarget = JavaVersion.VERSION_1_8
    }
  }

  compileTestKotlin {
    kotlinOptions {
      jvmTarget = JavaVersion.VERSION_1_8
    }
  }

  compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  sourceSets {
    main.resources.srcDirs += "src/main/proto/"

    main.kotlin {
      srcDirs += "$buildDir/generated/source/wire/"
    }
  }

  wire {
    kotlin {
      javaInterop = true
    }
  }

  repositories {
    mavenCentral()
    jcenter()
  }

  spotless {
    kotlin {
      target "**/*.kt"
      ktlint(dep.ktlintVersion).userData(['indent_size': '2', 'continuation_indent_size' : '4'])
    }
  }
  compileKotlin.dependsOn 'spotlessKotlinApply'

  test {
    useJUnitPlatform()
    testLogging {
      events "started", "passed", "skipped", "failed"
      exceptionFormat = 'full'
      showExceptions = true
      showStackTraces = true
    }
  }

  // We have to set the dokka configuration after evaluation since the com.vanniktech.maven.publish
  // plugin overwrites our dokka configuration on projects where it's applied.
  afterEvaluate { project ->
    project.tasks.dokka {
      reportUndocumented = false
      skipDeprecated = true
      jdkVersion = 8
    }
  }

  if (rootProject.file("hooks.gradle").exists()) {
    apply from: rootProject.file("hooks.gradle")
  }
}
